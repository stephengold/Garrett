// Gradle build script for the Garrett project

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    // module coordinates of external dependencies:
    heartCoordinates = 'com.github.stephengold:Heart:9.0.0'
    minieCoordinates = 'com.github.stephengold:Minie:8.0.0'

    // current versions of libraries:
    garrettVersion = '0.5.4-SNAPSHOT'
    jme3Version = '3.6.1-stable'
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

tasks.register('checkstyle') {
    dependsOn ':GarrettExamples:checkstyleMain', ':GarrettLibrary:checkstyleMain'
    description 'Checks the style of all Java sourcecode.'
}

// Register publishing tasks:

tasks.register('install') {
    dependsOn ':GarrettLibrary:install'
    description 'Installs Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':GarrettLibrary:release'
    description 'Stages Maven artifacts to Sonatype OSSRH.'
}
