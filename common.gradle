// Gradle settings and tasks common to all Garrett subprojects

apply plugin: 'checkstyle'
apply plugin: 'java'

ext {
    // module coordinates of dependencies:
    heartCoordinates = 'com.github.stephengold:Heart:8.0.0'
    minieCoordinates = 'com.github.stephengold:Minie:5.0.0'

    // current versions of libraries:
    garrettVersion = '0.4.1-SNAPSHOT'
    jme3Version = '3.5.2-stable'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_14)) {
        // Suppress warnings that source value 7 is obsolete.
        options.compilerArgs << '-Xlint:-options'
    }
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_10)) {
        options.release = 8
    }
}

repositories {
    //mavenLocal() // to find local SNAPSHOTs of libraries
    mavenCentral()
}
